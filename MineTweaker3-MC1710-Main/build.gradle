import minetweaker.tasks.RegisterZenClassesTask;

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'org.ow2.asm:asm-debug-all:5.0.3'
        classpath 'com.google.guava:guava:18.0'
        classpath 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
        classpath 'com.cloudbees:diff4j:1.1' // for difing and patching
        classpath 'com.github.abrarsyed.jastyle:jAstyle:1.2' // formatting
        classpath 'net.sf.trove4j:trove4j:2.1.0' // because its awesome.
        classpath 'com.github.jponge:lzma-java:1.3' // replaces the LZMA binary
        classpath 'com.nothome:javaxdelta:2.0.1' // GDIFF implementation for BinPatches
        classpath 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
        classpath 'com.github.tony19:named-regexp:0.2.3' // 1.7 Named regexp features
        classpath 'net.md-5:SpecialSource:1.7.3' // deobf and reobs
        classpath 'org.apache.httpcomponents:httpclient:4.3.3'
        classpath 'org.apache.httpcomponents:httpmime:4.3.3'
        classpath 'de.oceanlabs.mcp:RetroGuard:3.6.6'
        classpath 'de.oceanlabs.mcp:mcinjector:3.2-SNAPSHOT'
        classpath 'net.minecraftforge.srg2source:Srg2Source:3.2-SNAPSHOT'
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-CUSTOM'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'

sourceCompatibility = targetCompatibility = 1.8

version = "1.7.10-" + ext.mineTweakerVersion
archivesBaseName = project.projectDir.name

minecraft {
	version = forgeVersion1710
}

dependencies {
	dependencies {
		compile project(':ZenScript')
		compile project(':MineTweaker3-API')
	}
}

task makeRegistry(type: RegisterZenClassesTask) {
	inputDir = file('build/classes/main')
	outputDir = file('build/classes/main')
	className = 'minetweaker.mc1710.MineTweakerRegistry'
}

tasks.jar.dependsOn(makeRegistry)

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
	
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
            pom {
                groupId = project.group
                version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'CraftTweaker'
                    url 'https://github.com/jaredlll08/CraftTweaker'
                    scm {
                        url 'https://github.com/jaredlll08/CraftTweaker.git'
                        connection 'scm:git:git@github.com:jaredlll08/CraftTweaker.git'
                        developerConnection 'scm:git:git@github.com:jaredlll08/CraftTweaker.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/jaredlll08/CraftTweaker/issues'
                    }
                    developers {
                        developer {
                            id 'Jared'
                            name 'Jared'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
